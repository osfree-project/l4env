dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)

AC_INIT(src/main.cpp)
AM_INIT_AUTOMAKE(dice, 3.1.3)
AM_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST
dnl Test for Darwin host
AM_CONDITIONAL(HOST_DARWIN, [case "$host_os" in darwin*) true ;; *) false ;; esac])

dnl Test for AIX
AC_AIX

dnl dynamic load library support
dnl enable building convenience library
AC_LIBLTDL_CONVENIENCE
dnl check for dlopen support
AC_LIBTOOL_DLOPEN
dnl check for libtool
AM_PROG_LIBTOOL
dnl build libltdl subdir
AC_CONFIG_SUBDIRS(libltdl)
AC_SUBST(LTDLINCL)
AC_SUBST(LIBLTDL)

dnl Checks for programs.
AC_PROG_CXX
AC_SUBST(CXX)
AM_PROG_LEX
if test "$LEX" != flex; then
  AC_MSG_ERROR(['flex' is missing from your system.])
fi
dnl 
dnl check for bison
dnl
dnl 1) use generic check
dnl 2) check for specific bison binary if multiple are installed (-1.35)
dnl 3) check version of bison binary
dnl
AC_PROG_YACC
AC_CHECK_PROG(YACC, bison-1.35, bison-1.35 -y, bison -y)
if test "$YACC" = byacc; then
  AC_MSG_ERROR(['bison' is missing from your system.])
fi
if test "$YACC" = yacc; then
  AC_MSG_ERROR(['bison' is missing from your system.])
fi
AC_CACHE_CHECK([for bison version], dice_bison_version, [
  set `$YACC --version|grep 'GNU Bison'|cut -d ' ' -f 4|sed -e 's/\./ /'|tr -d a-z`
  if test "(" "${1}" = "1" -a "(" "${2}" = "35" -o "${2}" = "75" -o "${2}" = "875" ")" ")" -o "(" "${1}" = "2" ")"; then
    dice_bison_version="${1}.${2} (ok)"
  else
    dice_bison_version=invalid
  fi
])
case $dice_bison_version in
""|invalid[)]
  AC_MSG_ERROR([You will need at lest bison version 1.35 (found ${1}.${2}).])
  ;;
esac
AC_PROG_LN_S

dnl check for documentation programs
AC_CHECK_PROG(DOXYGEN,doxygen,doxygen)
AM_CONDITIONAL(HAS_DOXYGEN, test "x$DOXYGEN" != "x")

AC_CHECK_PROG(LATEX,latex,latex)
AC_CHECK_PROG(BIBTEX,bibtex,bibtex)
AC_CHECK_PROG(DVIPS,dvips,dvips)
AC_CHECK_PROG(FIG2DEV,fig2dev,fig2dev)
AC_CHECK_PROG(PDFLATEX,pdflatex,pdflatex)
AC_CHECK_PROG(EPS2PDF,epstopdf,epstopdf)

AM_CONDITIONAL(HAS_LATEX, test "x$LATEX" != "x" -a "x$BIBTEX" != "x")
AM_CONDITIONAL(HAS_DVIPS, test "x$DVIPS" != "x")
AM_CONDITIONAL(HAS_FIG2DEV, test "x$FIG2DEV" != "x")
AM_CONDITIONAL(HAS_PDFLATEX, test "x$PDFLATEX" != "x")

dnl check headers
AC_CHECK_HEADER(getopt.h, 
	[AC_DEFINE(HAVE_GETOPT_H, 1, needed by non-GNU systems)])

dnl check types
AC_CHECK_SIZEOF(long long, 8)
	
dnl check functions:
dnl check for getopt version
AC_CHECK_FUNC(getopt_long_only, 
	[AC_DEFINE(HAVE_GETOPT_LONG, 1, needed by non-GNU systems)])
dnl check for atoll
AC_CHECK_FUNC(atoll, AC_DEFINE(HAVE_ATOLL, 1, [if your C-lib has atoll]))

dnl Remove generated Makefiles wthen doing maintainer-clean.
MAINTAINERCLEANFILES="$MAINTAINERCLEANFILES Makefile.in"
AC_SUBST(MAINTAINERCLEANFILES)

dnl Always include top level includes.
dnl turn on _all_ warnings.
CPPFLAGS="$CPPFLAGS -Wall -I\$(top_srcdir)/src"
AC_SUBST(CPPFLAGS)

dnl Check for debugging
AC_ARG_ENABLE(debug,
[  --disable-debug         disable debugging code (assert, etc.)],
[case "${enableval}" in
yes) debug=true ;;
no) debug=false ;;
*) AC_MSG_ERROR(bad value ${enableval} for --disable-debug) ;;
esac],[debug=true])
if test "$debug" = "false" ; then
  CPPFLAGS="$CPPFLAGS -DNDEBUG"
  AC_SUBST(CPPFLAGS)
fi

dnl Check for verbose build
AC_ARG_ENABLE(verbose,
[  --disable-verbose        disable verbose build],
[case "${enableval}" in
yes) verbose=true ;;
no) verbose=false ;;
*) AC_MSG_ERROR(bad value ${enableval} for --disable-verbose) ;;
esac],[verbose=true])
if test "$verbose" = "false" ; then
  AM_MAKEFLAGS="$AM_MAKEFLAGS -s"
  AC_SUBST(AM_MAKEFLAGS)
fi

dnl check for different kernel features
dnl AM_CXXFLAGS = -Wunused-parameter -Wall -W -Wcast-align
dnl -Wcast-qual -Wconversion -Wformat=2 -Wmissing-format-attribute
dnl -Wpointer-arith -Wshadow -Wwrite-strings -Wstrict-aliasing=2
DICE_CXXFLAGS=""
for f in "-Wunused-parameter" "-Wall" "-W" "-Wcast-align" "-Wcast-qual" "-Wconversion" "-Wformat" "-Wformat=2" "-Wmissing-format-attribute" "-Wpointer-arith" "-Wshadow" "-Wwrite-strings" "-fstrict-aliasing" "-Wstrict-aliasing" "-Wstrict-aliasing=2"; do
    AC_MSG_CHECKING([$CXX supports $f])
    if `$CXX $f -o /dev/null -c -x c++ /dev/null >/dev/null 2>&1`; then
	AC_MSG_RESULT([yes])
	DICE_CXXFLAGS="$DICE_CXXFLAGS $f"
    else
	AC_MSG_RESULT([no])
    fi
done
AC_SUBST(DICE_CXXFLAGS)

dnl Modified files.
AC_CONFIG_FILES([
GNUmakefile
doc/Makefile
doc/manual/Makefile
doc/html/Makefile
doc/user-man/Makefile
src/Makefile
src/parser/Makefile
src/parser/dce/Makefile
src/parser/corba/Makefile
src/parser/inc/Makefile
src/parser/gcc-c/Makefile
src/parser/gcc-cxx/Makefile
src/fe/Makefile
src/be/Makefile
src/be/l4/Makefile
src/be/l4/v2/Makefile
src/be/l4/v2/amd64/Makefile
src/be/l4/v2/ia32/Makefile
src/be/l4/v4/Makefile
src/be/l4/v4/ia32/Makefile
src/be/sock/Makefile
include/Makefile
include/dice/Makefile
lib/Makefile
lib/debug/Makefile
lib/invocations/Makefile
lib/sizes/Makefile
lib/opcodes/Makefile
])

AC_OUTPUT
