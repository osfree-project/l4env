/* $Id$ */
/*! \file exec/idl/exec.idl
 * 
 * \date   08/22/2000
 * \author Frank Mehnert <fm3@os.inf.tu-dresden.de>
 * 
 * \brief Common L4 Environment, EXEC layer
 */

/* (c) 2003 Technische Universitaet Dresden
 * This file is part of DROPS, which is distributed under the terms of the
 * GNU General Public License 2. Please see the COPYING file for details. */

import <l4/sys/types.h>
import <l4/dm_generic/types.h>

/** l4 namespace. */
library l4exec
{
  /** structure that is exchanged by the interface. */
  typedef long envpage_t[563];

  /** exec interface. */
  interface bin
    {
      /** Open the binary file.
       * Get the file image from the file provider, analyse the header
       * information, and load all dependant libraries. The envpage
       * describes what servers to use to open dataspaces. */
      long open([in, string] char* fname, [in] l4dm_dataspace_t *img_ds,
		[in, out] envpage_t *envpage, [in] unsigned long flags);

      /** Test file type of image.
       * \return   0 if interpreter knows how to handle it.
       * 	 -L4_EXEC_INTERPRETER if image contains an interpreter section
       * 	 -L4_EXEC_BADFORMAT if image format is unknown. */
      long ftype([in] l4dm_dataspace_t *img_ds, [in] envpage_t *envpage);

      /** Close binary. Release all related memory. */
      long close([in, out] envpage_t *envpage);

      /** Relocate/link the binary described by envpage. */
      long link([in, out] envpage_t *envpage);

      /** Get debug symbols.
       * The dataspace is contiguous. */
      long get_symbols([in] envpage_t envpage,
		       [out] l4dm_dataspace_t *sym_ds);

      /** Get debug lines info.
       * The dataspace is contiguous. */
      long get_lines([in] envpage_t envpage,
		     [out] l4dm_dataspace_t *lines_ds);

      /** Get the address of a symbol. */
      long get_dsym([in] char *symname,
		    [in] envpage_t envpage,
		    [out] l4_addr_t *addr);
    };
};
