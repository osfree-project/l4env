SECTIONS
{
  /* Read-only sections, merged into text segment: */
  . = @linkaddr@;

  _start = .;
  PROVIDE (_start = .);
  
  _mbi = .;
  PROVIDE (_mbi = .);
  . += 0x4000;

  .init :
  {
    *(.init)
    init.o(.text .rodata .data .bss COMMON)
    lines.o(.text .rodata .data .bss COMMON)
    symbols.o(.text .rodata .data .bss COMMON)
    cfg-parse.o(.text .rodata .data .bss COMMON)
    exec.o(.text .rodata .data .bss COMMON)
    bootquota.o(.text .rodata .data .bss COMMON)
    boot_error.o(.text .rodata .data .bss COMMON)
  } =0x9090

  . = ALIGN(0x1000);

  _stext = .;

  .text :
  {
    *(.text)
  } =0x9090

  _etext = .;
  PROVIDE (etext = .);

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(0x1000) + ((ALIGN(8) + 0x1000 - ALIGN(0x1000)) & (0x1000 - 1));
  .data :
  {
    *(.rodata*)
    . = ALIGN(0x1000);
    _multi_boot_info = .;
    PROVIDE (_multi_boot_info = .);
    rmgr.o(.multi_boot_info)
    . = ALIGN(0x1000);
    _cmdline = .;
    PROVIDE (_cmdline = .);
    rmgr.o(.cmdline)
    _mod_names = .;
    PROVIDE (_mod_names = .);
    rmgr.o(.mod_names)
    *(.data)
    CONSTRUCTORS
  }
  _edata  =  .;
  PROVIDE (edata = .);
  __bss_start = .;

  /* The __memmap and __pool arrays must be located right before _end! */
  .bss :
  {
   *(.bss)
   *(COMMON)
   *(.bss_memmap)
   *(.bss_pool)
  }
  _end = . ;
  PROVIDE (_end = .);

  /DISCARD/ : {
    *(.interp)
    *(.comment)
    *(.note)
    *(.eh_frame)
  }
}
