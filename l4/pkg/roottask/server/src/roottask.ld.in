SECTIONS
{
  /* Read-only sections, merged into text segment: */
  . = @linkaddr@;

  _start = .;
  PROVIDE (_start = .);
  
  .init :
  {
    *(.init)
    init.o(.text .rodata .data)
    lines.o(.text .rodata .data)
    symbols.o(.text .rodata .data)
    cfg-parse.o(.text .rodata .data)
    exec.o(.text .rodata .data)
    bootquota.o(.text .rodata .data)
    boot_error.o(.text .rodata .data)
    module.o(.text .rodata .data)
    *cp-demangle.o(.text .rodata .data)
    *cplus-dem.o(.text .rodata .data)
    *safe-ctype.o(.text .rodata .data)
  } =0x9090

  .init.bss :
  {
    init.o(.bss COMMON)
    lines.o(.bss COMMON)
    symbols.o(.bss COMMON)
    cfg-parse.o(.bss COMMON)
    exec.o(.bss COMMON)
    bootquota.o(.bss COMMON)
    boot_error.o(.bss COMMON)
    module.o(.bss COMMON)
    *cp-demangle.o(.bss COMMON)
    *cplus-dem.o(.bss COMMON)
    *safe-ctype.o(.bss COMMON)
  }

  . = ALIGN(0x1000);

  _stext = .;

  .text :
  {
    *(.text)
  } =0x9090

  _etext = .;
  PROVIDE (etext = .);

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(0x1000) + ((ALIGN(8) + 0x1000 - ALIGN(0x1000)) & (0x1000 - 1));
  .data :
  {
    *(.rodata*)
    . = ALIGN(0x1000);
    _multi_boot_info = .;
    PROVIDE (_multi_boot_info = .);
    rmgr.o(.multi_boot_info)
    . = ALIGN(0x1000);
    _cmdline = .;
    PROVIDE (_cmdline = .);
    rmgr.o(.cmdline)
    _mod_names = .;
    PROVIDE (_mod_names = .);
    rmgr.o(.mod_names)
    *(.data)
    CONSTRUCTORS
  }
  _edata  =  .;
  PROVIDE (edata = .);
  __bss_start = .;

  /* The __memmap and __pool arrays must be located right before _end! */
  .bss :
  {
   *(.bss)
   *(COMMON)
   *(.bss_memmap)
   *(.bss_pool)
  }
  _end = . ;
  PROVIDE (_end = .);

  /DISCARD/ : {
    *(.interp)
    *(.comment)
    *(.note)
    *(.eh_frame)
  }
}
