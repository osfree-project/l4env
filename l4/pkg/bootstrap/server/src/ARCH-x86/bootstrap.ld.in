SECTIONS
{
  /* Read-only sections, merged into text segment: */
  . = LINKADDR;

  .init :
  {
    *(.init)
    startup.o(.text .rodata .data .bss COMMON)
    ARCH-x86/serial.o(.text .rodata .data .bss COMMON)
  } =0x9090

  . = ALIGN(0x1000);

  _stext = .;

  .text :
  {
    *(.text)
  } =0x9090

  _etext = .;
  PROVIDE (etext = .);

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  /* . = ALIGN(0x1000) + ((ALIGN(8) + 0x1000 - ALIGN(0x1000)) & (0x1000 - 1)); */
  . = ALIGN(0x1000);
  .data :
  {
    *(.rodata*)
    *(.data)

    _module_info_start = .;
    *(.module_info)
    _module_info_end = .;

    . = ALIGN(4K);
    _modules_start = .;
    *(.module.data)
    . = ALIGN(4K);
    _modules_end = .;
    
    CONSTRUCTORS
  }
  _edata  =  .;
  PROVIDE (edata = .);
  . = ALIGN(0x1000);
  __bss_start = .;
  .bss :
  {
   *(.bss)
   *(COMMON)
   *(.bss_memmap)
  }
  _end = . ;
  PROVIDE (end = .);

/* Only move modules to modaddr if bootstrap is being loaded by an elf
 * loader */
#ifndef SINGLE_SECTION
  . = MODADDR;
#endif
  _module_data_start = .;
  .module_data : { *(.module_data) }
  _module_data_end = .;

  /DISCARD/ : {
    *(.interp)
    *(.comment)
    *(.note)
    *(.eh_frame)
  }
}
