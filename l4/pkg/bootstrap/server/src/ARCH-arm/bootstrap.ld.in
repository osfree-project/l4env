/* -*- Makefile -*- */
OUTPUT_FORMAT("elf32-littlearm", "elf32-bigarm",
              "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_start)

SECTIONS
{
  /* Read-only sections, merged into text segment. The start address of
   * the text segment is : */
  .text :
  {
    _stext = .;
    *(.text.init) 
    *(.init)
    *(.text .text.* .gnu.linkonce.t.*)
    *(.glue_7t) *(.glue_7)
    KEEP (*(.fini))
    LONG(0xc3)  /* terminate .fini */
    . = ALIGN(0x40);
    *(.rodata .rodata.* .gnu.linkonce.r.* .rodata1)
  }

  _etext = .;
  PROVIDE (etext = .);

  /* ensure that data starts at a new L4 page */
  . = ALIGN(4K);
  .data :
  {
    *(.data .data.* .gnu.linkonce.d.*)
    *(.anno)

    . = ALIGN(4);
    _module_info_start = .;
    *(.module_info)
    _module_info_end   = .;

    . = ALIGN(4K);
    _modules_start = .;
    *(.module.data)
    . = ALIGN(4K);
    _modules_end   = .;
  }
  /* exception frames for C++ */
  .eh_frame : 
  { 
    KEEP (*(.eh_frame)) 
    LONG(0) /* terminate .eh_frame */
  }

  . = ALIGN(4);
  _edata  =  .;
  PROVIDE (edata = .);

  _bss_start = .;
  .bss :
  {
   *(.dynbss)
   *(.bss .gnu.linkonce.b.*)
   *(COMMON)
  }
  _bss_end = .;
  _end = . ;
  PROVIDE (end = .);

  /* Moved here to ensure that these sections are located _after_ the text
   * section. In the other case we would get program sections with a virtual
   * address of 0 */
  .hash    : { *(.hash)    }
  .dynsym  : { *(.dynsym)  }
  .dynstr  : { *(.dynstr)  }
  .rel.dyn : { *(.rel.dyn) }

  . = ALIGN(4K);
  .module_data : { 
    _module_data_start = .;
    *(.module_data)
    _module_data_end = .;
  }

  /* drop the following sections since we do not need them for DROPS */
  /DISCARD/ : {
    *(.interp)
    *(.comment)
    *(.note)
    *(.stab)
  }
}

